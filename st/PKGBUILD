pkgname=st
pkgver=0.4.1
pkgrel=1
pkgdesc="A simple terminal implementation for X."
url="http://st.suckless.org"
arch=('i686' 'x86_64')
license=('MIT')
makedepends=('ncurses')
depends=('libxext' 'libxft')

install=st.install
source=(http://dl.suckless.org/st/st-$pkgver.tar.gz
	config.h)
md5sums=('fa03d702b6d67de395975155c87084e9'
         'e549263d2db2dc725fa68c17a09de488')

build() {
  cd $srcdir/$pkgname-$pkgver
  cp $srcdir/config.h config.h
  sed -i 's/CPPFLAGS =/CPPFLAGS +=/g' config.mk
  sed -i 's/^CFLAGS = -g/#CFLAGS += -g/g' config.mk
  sed -i 's/^#CFLAGS = -std/CFLAGS += -std/g' config.mk
  sed -i 's/^LDFLAGS = -g/#LDFLAGS += -g/g' config.mk
  sed -i 's/^#LDFLAGS = -s/LDFLAGS += -s/g' config.mk
  make X11INC=/usr/include/X11 X11LIB=/usr/lib/X11
}

package() {
  cd $srcdir/$pkgname-$pkgver
  make PREFIX=/usr DESTDIR=$pkgdir install
  install -m644 -D LICENSE $pkgdir/usr/share/licenses/$pkgname/LICENSE
  install -m644 -D README $pkgdir/usr/share/doc/$pkgname/README
}
