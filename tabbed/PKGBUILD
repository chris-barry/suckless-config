# $Id: PKGBUILD 92069 2013-05-31 08:05:10Z lfleischer $
# Maintainer: Lukas Fleischer <archlinux at cryptocrack dot de>
# Contributor: totoloco <totoloco at gmx>

pkgname=tabbed
pkgver=0.5
pkgrel=1
pkgdesc='Simple generic tabbed fronted to xembed aware applications.'
arch=('i686' 'x86_64')
url='http://tools.suckless.org/tabbed'
license=('custom:MIT/X')
depends=('libx11')
install='tabbed.install'
source=("http://dl.suckless.org/tools/${pkgname}-${pkgver}.tar.gz"
        'config.h')
md5sums=('cd9cfd696db745637486ec3b9bbe4b75'
         '119d03736c9a3762ed2f2cfa8aa47ca1')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  cp "${srcdir}/config.h" config.h

  sed -i 's/CPPFLAGS =/CPPFLAGS +=/g' config.mk
  sed -i 's/CFLAGS =/CFLAGS +=/g' config.mk
  sed -i 's/LDFLAGS =/LDFLAGS +=/g' config.mk

  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  make PREFIX=/usr DESTDIR="${pkgdir}" install
  install -Dm0644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
